# Maintainer: shmilee <echo c2htaWxlZS56anVAZ21haWwuY29tCg==|base64 -d>

_pkgname=wps-office
_pkgver='11.1.0.11664'
_srcurl='https://wps-linux-personal.wpscdn.cn/wps/download/ep/Linux2019/11664'
_common_dirs=(
    ./etc
    ./usr
    ./opt/kingsoft/wps-office/desktops
    ./opt/kingsoft/wps-office/office6/addons/officespace/mui
    ./opt/kingsoft/wps-office/office6/{cfgs,data,dicts,mui,res,skins,wpsart}
    ./opt/kingsoft/wps-office/templates
)

pkgbase=wpsforlinux
pkgname=('wpsoffice-common'
         'wpsoffice')
pkgver=${_pkgver/\~/_}
pkgrel=2
arch=('x86_64')
groups=('wpsforlinux')
license=('custom:Kingsoft')
url='http://wps-community.org/'
conflicts=('wps-office' 'kingsoft-office')
options=(!emptydirs !strip)
source=('wps-office.xml' 'check_data.sh' "${_srcurl}/${_pkgname}_${_pkgver}_amd64.deb")
sha1sums=('0b8f9548bb77bfa468f658e2bcb14960badc6e9e'
          '1819bae14fa888f92c29c9fd01b59246f53e6937'
          '4cd45a776ab31fef01281eac45cb3aca080249df')

#PKGEXT='.pkg.tar' ##reduce the time of compression

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;\
text\/csv;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/msword-template;application\/rtf;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;"

package_wpsoffice-common() {
    arch=('any')
    pkgdesc="Common files and license for WPS office."

    cd "$srcdir"
    # _common_dirs
    tar xv -C "${pkgdir}" -f data.tar.xz ${_common_dirs[@]}
    tar xv -C "${pkgdir}" -f data.tar.xz './opt/kingsoft/wps-office/office6/document theme'

    msg2 "editing mime files: wps-office-{et,wpp,wps,pdf}.xml ..."
    rm -f "$pkgdir"/usr/share/mime/packages/wps-office-{wps,wpp,et,pdf}.xml
    install -Dm644 wps-office.xml "$pkgdir"/usr/share/mime/packages/wps-office.xml

    msg2 "editing desktop files: wps-office-{et,wpp,wps}.desktop, with shared-mime-info ..."
    rm -rf "${pkgdir}/opt/kingsoft/wps-office/desktops/"
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" "$pkgdir"/usr/share/applications/wps-office-et.desktop
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" "$pkgdir"/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" "$pkgdir"/usr/share/applications/wps-office-wps.desktop

    # fix: lost menu icon
    cd "${pkgdir}/usr/share/icons/hicolor"
    for _size in *; do
        install -dm755 "${_size}/apps"
        find "${_size}/mimetypes" -name 'wps-office2019-*main.*' -exec mv {} "${_size}/apps" \;
    done

    # fix: BOM
    cd "$pkgdir"/usr/share/applications/
    if grep -r -I -l $'^\xEF\xBB\xBF' *.desktop 2>&1 >/dev/null; then
        msg2 "Remove BOM (Byte-order mark) in utf-8 .desktop files ..."
        sed -i 's/^\xEF\xBB\xBF//;' *.desktop
    fi

    # fix wps update log logrotate error
    # sed 's|su root|su root root|' "$pkgdir"/etc/logrotate.d/wpsupdatelog
    # or rm wps update
    cd "$pkgdir"/etc/
    rm -rf cron.d/ logrotate.d/ xdg/autostart/

    # license
    for _ext in "txt" "html"; do
        if [ -f "$pkgdir/opt/kingsoft/wps-office/office6/mui/default/EULA_linux.${_ext}" ]; then
            install -dm755 "$pkgdir/usr/share/licenses/${_pkgname}"
            ln -sv "/opt/kingsoft/wps-office/office6/mui/default/EULA_linux.${_ext}" \
                "$pkgdir/usr/share/licenses/${_pkgname}/EULA.${_ext}"
            break
        fi
    done

    clean_startpage_qing "${pkgdir}/opt/kingsoft/wps-office/office6/"
}

package_wpsoffice() {
    arch=('x86_64')
    pkgdesc='WPS Office, including Writer, Presentation and Spreadsheets, is a powerful office productivity suite. (no fonts)'
    depends=('fontconfig' 'xorg-mkfontdir' 'libxrender'
             'desktop-file-utils' 'shared-mime-info' 'xdg-utils'
             'glu' 'openssl-1.1' 'sdl2' 'libpulse' 'hicolor-icon-theme'
             'libxss' 'sqlite' 'libtool' 'libxslt' 'qt5-base'
             "wpsoffice-common=${pkgver}")
    optdepends=('cups: for printing support'
                'libjpeg-turbo: JPEG image codec support'
                'pango: for complex (right-to-left) text support'
                'curl: An URL retrieval utility and library')
    install=wpsoffice.install

    cd "${srcdir}"
    tar xv -C "${pkgdir}" -f data.tar.xz ${_common_dirs[@]/\.\//--exclude=\.\/} \
        --exclude='./opt/kingsoft/wps-office/office6/document theme'

    # fix libstdc++.so.6: version `GLIBCXX_3.4.30' not found (required by /usr/lib/libicuuc.so.72)
    rm "${pkgdir}"/opt/kingsoft/wps-office/office6/libstdc++.so*

    clean_startpage_qing "${pkgdir}/opt/kingsoft/wps-office/office6/"
}

# rm built-in(internal) browser, startpage etc.
# 去除左上角主图标及设置, 云文档, 登录, 轻办公等需要浏览器插件的功能
# 修改设置默认浏览器打开
# sqlite3 ~/.local/share/Kingsoft/wps/addons/data/win-i386/promebrowser/Preferences \
#   'update global_preference set useDefaultBrowser=1 where id=1;'
clean_startpage_qing() {
    # office6_path="$1"
    cd "$1"
    rm -rf libcef.so addons/cef/
    # addons/k* qing
    mkdir ./keep_sodir_tmp  # keep_sodir
    for sodir in krecentfile kfpccomb kmodule knetwork konlinefileconfig; do
        if [ -d "addons/${sodir}" ]; then
            cp -rv "addons/${sodir}" ./keep_sodir_tmp/
        fi
    done
    rm -rf addons/{k*,qing,pdfbatchcompression,wpsbox}
    if [ -n "$(ls -A ./keep_sodir_tmp)" ]; then
        mv -v ./keep_sodir_tmp/* addons/
    fi
    rmdir ./keep_sodir_tmp
    find . -name qing.qm -exec rm {} \;
    # others
    rm -rf wpsoffice wpsoffice-off
    for back in transerr wpscloudsvr; do
        if [ -f "${back}" ]; then
            chmod -x "${back}"
            mv -v "${back}" "${back}.bk"
        fi
    done
}
