# Maintainer: shmilee <shmilee.zju@gmail.com>
# Maintainer: Purofle <purofle@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

_LiteLoader_VER=1.2.2
#PKGEXT='.pkg.tar'

pkgbase=linuxqq
pkgname=("$pkgbase" "$pkgbase-liteloader")
pkgver=3.2.12_28327
pkgrel=1
epoch=5
pkgdesc="New Linux QQ based on Electron"
arch=('x86_64')  # 'aarch64' 'loong64')
url="https://im.qq.com/linuxqq"
license=('LicenseRef-QQ')
conflicts=("linuxqq-nt-bwrap")
depends=('nss' 'alsa-lib' 'gtk3' 'gjs' 'at-spi2-core' 'libvips' 'openjpeg2' 'openslide')
optdepends=(
    'libappindicator-gtk3: Allow QQ to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).'
    'linuxqq-liteloader: LiteLoaderQQNT plugins support'
)
source_x86_64=("https://dldir1.qq.com/qqfile/qq/QQNT/f60e8252/linuxqq_${pkgver/_/-}_amd64.deb")
#source_aarch64=("https://dldir1.qq.com/qqfile/qq/QQNT/f60e8252/linuxqq_${pkgver/_/-}_arm64.deb")
#source_loong64=("https://dldir1.qq.com/qqfile/qq/QQNT/f60e8252/linuxqq_${pkgver/_/-}_loongarch64.deb")
source=(
    "linuxqq.sh"
    "app_launcher-index-lite.js"
    "LiteLoaderQQNT-${_LiteLoader_VER}.zip::https://github.com/LiteLoaderQQNT/LiteLoaderQQNT/releases/download/${_LiteLoader_VER}/LiteLoaderQQNT.zip"
)
noextract=("LiteLoaderQQNT-${_LiteLoader_VER}.zip")
sha512sums=('0ceec5e467471e6e4a654254ef4e9ea22bdfa6c6f53820ce83fc2ae2a55cd5347d16cd47b868f6681e39d472b71ac0f15c32a36b82425a3a52da0d363c4b6e0e'
            '991d7b89511f3023cf96d967e475e8a6deb1ca3ca079c7cbf3455be03e9c89adb723956ad05129059f8093fb252718478e5faec3ee1590487f32ba8160dfde2c'
            '4a36f5906f17eb2139536a2ce97a31da9c0509309665e35c1f4e28bb93984a7a24bbf5f17aa754629b38eb430eacf6d3eb9dc9a7ad9f6dd24deb1917f926f6da')
sha512sums_x86_64=('5702f246cf896360dceac9d5b49a5289656e6c834e9835c5eac4025eb923655a1abe8e7eeca510a3f0f0069e634cb5d1a3a009b38c261cb0c7c941c78b152201')
#sha512sums_aarch64=('122898bee3b3d986ef75e55b359b5eb359caf0e76fe06dfb8fe19deac6b7a3a494be0444ce03bd5c5020f0fd052a166e39acb074969217f9429b3481cc3b97de')
#sha512sums_loong64=('64a0e6b27df4d665a955365901f41a4d7d67e055e4421af96e134a9c2ca439f466731b001c3a18f0a3a2e5669b5d3970dbdc12a756fa453f18d5a335cf0b537c')
options=('!strip' '!debug')

package_linuxqq() {
    echo "  -> Extracting the data.tar.xz..."
    bsdtar -xf data.tar.xz -C "${pkgdir}/"
    rm -f "${pkgdir}/opt/QQ/resources/app/libssh2.so.1" # Temporary Fix

    echo "  -> Installing..."
    # Launcher
    install -Dm755 "linuxqq.sh" "${pkgdir}/usr/bin/linuxqq"

    # Launcher Fix
    sed -i '3s!/opt/QQ/qq!linuxqq!' "${pkgdir}/usr/share/applications/qq.desktop"

    # support for LiteLoaderQQNT
    # https://liteloaderqqnt.github.io/guide/install.html
    install -Dm644 "app_launcher-index-lite.js" "${pkgdir}/opt/QQ/resources/app/app_launcher/index-lite.js"
    sed -e 's|"main":.*js",|"main": "./app_launcher/index-lite.js",|' \
        -i "${pkgdir}/opt/QQ/resources/app/package.json"

    # Icon Fix
    sed -i '6s!/usr/share/icons/hicolor/512x512/apps/qq.png!qq!' "${pkgdir}/usr/share/applications/qq.desktop"

    # License
    install -Dm644 "${pkgdir}/opt/QQ/LICENSE.electron.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -Dm644 "${pkgdir}/opt/QQ/LICENSES.chromium.html" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

    # Temporary Solution: Remove libvips which comes from package "linuxqq" itself
    # 28327: use libvips from QQ
    #rm -f "${pkgdir}/opt/QQ/resources/app/sharp-lib/libvips-cpp.so.42"
}

package_linuxqq-liteloader() {
    pkgdesc="QQNT 插件加载器 LiteLoaderQQNT ${_LiteLoader_VER}"
    arch=('any')
    url="https://liteloaderqqnt.github.io"
    license=('MIT')
    conflicts=()
    depends=('linuxqq')
    optdepends=()

    # ref https://liteloaderqqnt.github.io/guide/install.html
    install -dm755 "${pkgdir}/opt/QQ/resources/app/LiteLoaderQQNT"
    unzip "LiteLoaderQQNT-${_LiteLoader_VER}.zip" -d "${pkgdir}/opt/QQ/resources/app/LiteLoaderQQNT"
}
