# Maintainer: shmilee <echo c2htaWxlZS56anVAZ21haWwuY29tCg==|base64 -d>

_pkgname=wps-office
_pkgver='11.1.0.8392'
_srcurl='http://kdl.cc.ksosoft.com/wps-community/download/8392'
_common_dirs=(
    ./etc
    ./usr
    ./opt/kingsoft/wps-office/office6/addons/{kwpsartstyleborder/resource,officespace/mui,qing/mui}
    ./opt/kingsoft/wps-office/office6/{cfgs,data,dicts,mui,res,skins,wpsart}
    ./opt/kingsoft/wps-office/templates
)

pkgbase=wpsforlinux
pkgname=('wpsoffice-common'
         'wpsoffice')
pkgver=${_pkgver/\~/_}
pkgrel=2
arch=('any')
groups=('wpsforlinux')
license=('custom:Kingsoft')
url='http://wps-community.org/'
conflicts=('wps-office' 'kingsoft-office')
options=(!emptydirs !strip)
source=('wps-office.xml' 'check_data.sh')
sha1sums=('dfa2ae8b74ac763d6d7e1d28e33dcb289bca9112' '1819bae14fa888f92c29c9fd01b59246f53e6937')
source_i686=("${_srcurl}/${_pkgname}_${_pkgver}_i386.deb")
sha1sums_i686=('60b1c9e33ee6fc1edcefe40dc9ec529d4a668825')
source_x86_64=("${_srcurl}/${_pkgname}_${_pkgver}_amd64.deb")
sha1sums_x86_64=('edb1bc215e46c46bb979869e374788498486b56c')

#PKGEXT='.pkg.tar' ##reduce the time of compression

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;\
text\/csv;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/msword-template;application\/rtf;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;"

package_wpsoffice-common() {
    arch=('any')
    pkgdesc="Common files and license for WPS office."

    cd "$srcdir"
    # _common_dirs
    tar xv -C "${pkgdir}" -f data.tar.xz ${_common_dirs[@]}
    tar xv -C "${pkgdir}" -f data.tar.xz './opt/kingsoft/wps-office/office6/document theme'

    msg2 "editing mime files: wps-office-{et,wpp,wps}.xml ..."
    rm "$pkgdir"/usr/share/mime/packages/wps-office-{wps,wpp,et}.xml
    install -Dm644 wps-office.xml "$pkgdir"/usr/share/mime/packages/wps-office.xml

    msg2 "editing desktop files: wps-office-{et,wpp,wps}.desktop, with shared-mime-info ..."
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" "$pkgdir"/usr/share/applications/wps-office-et.desktop
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" "$pkgdir"/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" "$pkgdir"/usr/share/applications/wps-office-wps.desktop

    # fix: BOM
    cd "$pkgdir"/usr/share/applications/
    if grep -r -I -l $'^\xEF\xBB\xBF' *.desktop 2>&1 >/dev/null; then
        msg2 "Remove BOM (Byte-order mark) in utf-8 .desktop files ..."
        sed -i 's/^\xEF\xBB\xBF//;' *.desktop
    fi

    # fix wps update log logrotate error
    # sed 's|su root|su root root|' "$pkgdir"/etc/logrotate.d/wpsupdatelog
    # or rm wps update
    cd "$pkgdir"/etc/
    rm -rf cron.d/ logrotate.d/ xdg/autostart/

    # license
    install -Dm644 "$pkgdir/opt/kingsoft/wps-office/office6/mui/default/EULA_linux.txt" \
        "$pkgdir/usr/share/licenses/${_pkgname}/EULA.txt"
}

package_wpsoffice() {
    arch=('i686' 'x86_64')
    pkgdesc='WPS Office, including Writer, Presentation and Spreadsheets, is a powerful office productivity suite. (no fonts)'
    depends=('fontconfig' 'xorg-mkfontdir' 'libxrender'
             'desktop-file-utils' 'shared-mime-info' 'xdg-utils'
             'glu' 'openssl-1.0' 'sdl2' 'libpulse' 'hicolor-icon-theme'
             "wpsoffice-common=${pkgver}")
    optdepends=('cups: for printing support'
                'libjpeg-turbo: JPEG image codec support'
                'pango: for complex (right-to-left) text support'
                'curl: An URL retrieval utility and library')
    install=wpsoffice.install

    cd "${srcdir}"
    tar xv -C "${pkgdir}" -f data.tar.xz ${_common_dirs[@]/\.\//--exclude=\.\/} \
        --exclude='./opt/kingsoft/wps-office/office6/document theme'
}
