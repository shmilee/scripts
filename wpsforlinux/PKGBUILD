# Maintainer: shmilee <echo c2htaWxlZS56anVAZ21haWwuY29tCg==|base64 -d>

_pkgname=wps-office
_pkgver=9.1.0.4909~a16p1
_srcurl="http://kdl.cc.ksosoft.com/wps-community/download/a16"

pkgbase=wpsforlinux
pkgname=('wpsoffice-common'
         'wpsoffice')
pkgrel=1
arch=('any')
groups=('wpsforlinux')
license=('custom:Kingsoft')
url='http://wps-community.org/'
conflicts=('wps-office' 'kingsoft-office' 'wpsoffice-zh-cn')
source=("${_srcurl}/${_pkgname}_${_pkgver}_i386.deb" 'wps-office.xml')
sha1sums=('e4648d59efa506506d6bf8670661e65e66030e33'
          'dfa2ae8b74ac763d6d7e1d28e33dcb289bca9112')

#PKGEXT='.pkg.tar' ##reduce the time of compression

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;\
text\/csv;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/msword-template;application\/rtf;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;"

package_wpsoffice-common() {
    pkgver=1.2
    arch=('i686' 'x86_64')
    pkgdesc="Dependencies and license for WPS office."
    if [ "$CARCH" = "i686" ]; then
        depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender' 'glu')
        optdepends=('libcups: CUPS printing support')
    elif [ "$CARCH" = "x86_64" ]; then
        depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender' 'lib32-glu')
        optdepends=('lib32-libcups: CUPS printing support')
    fi

    cd "$srcdir"
    tar xv -f data.tar.xz ./opt/kingsoft/wps-office/office6/mui/default/EULA.txt
    install -Dm644 ./opt/kingsoft/wps-office/office6/mui/default/EULA.txt "$pkgdir/usr/share/licenses/${_pkgname}/EULA.txt"
}

pkgver=${_pkgver}

package_wpsoffice() {
    arch=('any')
    pkgdesc='WPS Office, including Writer, Presentation and Spreadsheets, is a powerful office productivity suite, which is deeply compatible with all of the latest Microsoft Office file formats.'
    depends=('desktop-file-utils' 'shared-mime-info' 'xdg-utils' 'wpsoffice-common=1.2')
    optdepends=('wpsoffice-langpack: additional language support')
    options=(!strip)
    install=wpsoffice.install 
    
    cd "${srcdir}"
    tar xv -f data.tar.xz -C "${pkgdir}"

    msg2 "editing mime files: wps-office-{et,wpp,wps}.xml ..."
    rm $pkgdir/usr/share/mime/packages/wps-office-{wps,wpp,et}.xml
    install -Dm644 wps-office.xml $pkgdir/usr/share/mime/packages/wps-office.xml

    msg2 "editing desktop files: wps-office-{et,wpp,wps}.desktop, with shared-mime-info ..."
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop

    cd $pkgdir/usr/share/applications/
    if grep -r -I -l $'^\xEF\xBB\xBF' *.desktop 2>&1 >/dev/null; then
        msg2 "Remove BOM (Byte-order mark) in utf-8 .desktop files ..."
        sed -i 's/^\xEF\xBB\xBF//;' *.desktop
    fi
}
