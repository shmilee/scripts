# Maintainer: shmilee
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>

pkgname=goldendict
pkgver="1.5.0"
pkgrel=4
pkgdesc='A feature-rich dictionary lookup program, supporting multiple dictionary formats and online dictionaries'
arch=('x86_64')
url="https://github.com/goldendict/goldendict"
license=('GPL3')
depends=(
    'libvorbis' 'libxtst' 'libao' 'libtiff'
    #'libeb'  # for epwing etc.
    'qt5-webkit' 'qt5-svg' 'qt5-x11extras' 'qt5-tools' 'qt5-multimedia'
    'hunspell' 'ffmpeg' 'opencc'
    'bzip2' 'lzo' 'xz' 'zlib' 'zstd'
)
source=(
    "${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
    hidpi-icon.patch
)
sha256sums=('f9f8e068ece74cc026fdc5f56615246228b4e70c97c7197ccd4c14ceae412c7c'
            '635759e4e4e3049bd5cb2f8b72878826266a62446cf85737f490789b808c9b5d')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i "$srcdir"/hidpi-icon.patch
    msg "Fixing QMake in goldendict.pro file..."
    echo "QMAKE_CXXFLAGS_RELEASE = ${CFLAGS}" >> goldendict.pro
    echo "QMAKE_CFLAGS_RELEASE = ${CXXFLAGS}" >> goldendict.pro
    msg "Setting default version string..."
    sed -i "s/^\(VERSION = .*\)+git/\1/" goldendict.pro
    qmake-qt5 PREFIX='/usr' \
        'CONFIG+=no_epwing_support' \
        'CONFIG+=chinese_conversion_support' \
        'CONFIG+=zim_support' \
        goldendict.pro
}

build(){
    cd "${pkgname}-${pkgver}"
    make ${MAKEFLAGS} -j4
}

package() {
    cd "${pkgname}-${pkgver}"
    make INSTALL_ROOT="${pkgdir}" install
}
